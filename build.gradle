/*
 * Created on Sun Aug 08 2021
 *
 * Copyright (c) storycraft. Licensed under the MIT Licence.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'sh.pancake'
version '0.1.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

repositories {
    mavenCentral()

    maven {
        name = 'Sponge'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        name = 'Mojang'
        url = 'https://libraries.minecraft.net'
    }

    maven {
        name = 'StoryNetwork'
        url = 'https://raw.githubusercontent.com/story-network/maven/master/'
    }
}

dependencies {
    implementation 'sh.pancake:pancake-sauce:0.1.0'
    implementation 'sh.pancake:story-network-launch-util:0.1.0'

    implementation 'info.picocli:picocli:4.6.1'

    implementation 'org.apache.logging.log4j:log4j-core:2.8.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.8.1'
    implementation 'org.fusesource.jansi:jansi:1.18'

    // Mixin modding
    implementation('org.spongepowered:mixin:0.8') {
        exclude module: 'launchwrapper'
        exclude module: 'modlauncher'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }

    implementation 'org.ow2.asm:asm:7.1'
	implementation 'org.ow2.asm:asm-analysis:7.1'
	implementation 'org.ow2.asm:asm-commons:7.1'
	implementation 'org.ow2.asm:asm-tree:7.1'
    implementation 'org.ow2.asm:asm-util:7.1'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.named('test') {
    useJUnitPlatform()
}

java {
	withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }

        resources {
            srcDir 'src/resources'
        }
    }
}

jar {
    manifest {
        attributes 'Created-From': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')'
        attributes 'Main-Class': 'sh.pancake.launcher.main.Launcher'
    }
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

tasks.build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            name = 'StoryNetwork'
            url = uri('../maven')
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
