/*
 * Created on Sun Aug 08 2021
 *
 * Copyright (c) storycraft. Licensed under the MIT Licence.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'sh.pancake'
version '0.4.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

repositories {
    mavenCentral()

    maven {
        name = 'Sponge'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        name = 'Mojang'
        url = 'https://libraries.minecraft.net'
    }

    maven {
        name = 'StoryNetwork'
        url = 'https://raw.githubusercontent.com/story-network/maven/master/'
    }
}

dependencies {
    implementation 'sh.pancake:pancake-sauce:0.3.0'
    api 'sh.pancake:story-network-launch-util:0.1.0'

    implementation 'info.picocli:picocli:4.6.1'

    api 'org.apache.logging.log4j:log4j-core:2.8.1'
    api 'org.apache.logging.log4j:log4j-api:2.8.1'

    implementation 'org.fusesource.jansi:jansi:1.18'

    api 'com.google.code.findbugs:jsr305:3.0.2'

    // Mixin modding
    api('org.spongepowered:mixin:0.8') {
        exclude module: 'launchwrapper'
        exclude module: 'modlauncher'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }

    api 'org.ow2.asm:asm:9.2'
	api 'org.ow2.asm:asm-analysis:9.2'
	api 'org.ow2.asm:asm-commons:9.2'
	api 'org.ow2.asm:asm-tree:9.2'
    api 'org.ow2.asm:asm-util:9.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.named('test') {
    useJUnitPlatform()
}

java {
	withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }

        resources {
            srcDir 'src/resources'
        }
    }
}

jar {
    manifest {
        attributes 'Created-From': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')'
        attributes 'Main-Class': 'sh.pancake.launcher.main.Launcher'
    }
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

tasks.build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            name = 'StoryNetwork'
            url = uri('../maven')
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
